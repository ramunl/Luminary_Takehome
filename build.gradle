// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.koin_version = '2.0.1'
    ext.kotlin_version = '1.4.31'
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:4.1.2"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}
subprojects {
    if (it.name == 'app') {
        apply plugin: 'com.android.application'
    } else {
        apply plugin: 'com.android.library'
    }
    apply plugin: 'kotlin-android'
    apply plugin: 'kotlin-kapt'

    android {
        compileSdkVersion 30
        buildToolsVersion "30.0.3"
        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }
        defaultConfig {
            minSdkVersion 14
            targetSdkVersion 30
            multiDexEnabled true
            testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        }
    }

    dependencies {
        testImplementation 'junit:junit:4.13.2'
        androidTestImplementation 'androidx.test.ext:junit:1.1.2'
        androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

        implementation "androidx.core:core-ktx:1.3.2"
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

        // Koin for the dependencies injections
        implementation "org.koin:koin-android:$koin_version"
        implementation "org.koin:koin-android-viewmodel:$koin_version"
        implementation "org.koin:koin-androidx-scope:$koin_version"

        testImplementation "org.koin:koin-test:$koin_version"
        androidTestImplementation "org.koin:koin-test:$koin_version"

        // Coroutines for asynchronous calls (and Deferredâ€™s adapter)
        implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.3'
        implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.1'
        configurations {
            compile.exclude group: 'org.jetbrains.kotlinx', module: 'atomicfu-common'
        }
        implementation "androidx.multidex:multidex:2.0.1"

        // for room
        implementation 'androidx.room:room-runtime:2.2.6'
        annotationProcessor 'androidx.room:room-compiler:2.2.6'
        kapt 'androidx.room:room-compiler:2.2.6'
    }
}
task clean(type: Delete) {
    delete rootProject.buildDir
}